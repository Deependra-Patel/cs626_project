S	-> P | P END_SYM
P	-> SELECT | SELECT WHERE
END_SYM	-> '!' | '?' | '.'

SELECT	-> ACTION FIELD_

ACTION	-> DO | ASK

DO      -> VERB TARGET_ | VERB
VERB	-> 'get' | 'fetch' | 'select' | 'find' | 'show' | 'return' | 'display' | 'print' | 'give' | 'name' | 'list'
TARGET_	-> TARGET | TARGET CONJ1_ TARGET_ | TARGET CONJ1_
TARGET	-> AGENT_ | AGENT_ RELATE
AGENT_	-> AGENT | AGENT AGENT_
AGENT	-> 'me' | 'them' | 'these' | 'those' | 'him' | 'her' | 'us' | 'my' | 'our' | 'their' | 'your' | 'such'
RELATE	-> 'friend' | 'father' | 'mother' | 'son' | 'daughter' | 'brother' | 'sister' | 'dad' | 'mom' | 'bro'
CONJ1_	-> CONJ1 | CONJ1 CONJ1_
CONJ1	-> 'and' | 'or' | ',' | 'as_well_as' | 'as_well'
 
ASK		-> WH_Q | WH_Q HELP1_
WH_Q	-> 'what' | 'which' | 'who' | 'where' | 'whose' | 'when' | 'how' | 'why'
HELP1_	-> HELP1 | HELP1 HELP1_
HELP1	-> 'is' | 'are' | 'was' | 'were' | 'there'

FIELD_	-> FIELD | FIELD CONJ1_ FIELD_ | FIELD CONJ1_
FIELD	-> ATTRIB1_ ENTITY1_ | ATTRIB1_ | ENTITY1_ | FILLER_

ATTRIB1_ -> ATTRIB1 | ATTRIB1 CONJ1_ ATTRIB1_ | ATTRIB1 ATTRIB1_ | ATTRIB1 CONJ1_
ATTRIB1	-> FILLER_ ATTRIB2 | ATTRIB2
ATTRIB2	-> ATTRIB | INFO_WORD
ENTITY1_ -> ENTITY1 | ENTITY1 CONJ1_ ENTITY1_ | ENTITY1 ENTITY1_ | ENTITY1 CONJ1_
ENTITY1	-> FILLER_ ENTITY2 | ENTITY2 | FILLER_ STRESS ENTITY2 | STRESS ENTITY2
ENTITY2	-> ENTITY

STRESS	-> 'table' | 'relation' | 'data'
FILLER_	-> FILLER | FILLER FILLER_
FILLER	-> 'a' | 'an' | 'the' | 'some' | 'few' | 'any' | 'from' | 'of' | 'those' | 'belong' | 'belongs' | 'belonging' | 'to' | 'who' | 'which' | 'whose' | 'that' | 'were' | 'are' | 'have' | 'had' | 'has' | 'been' | 'is' | 'was' | 'by' | 'in' | 'among' | 'inside' | 'also' | 'their' | 'such' | 'it' | 'they' | 'out' | 'all' | 'as' | 'for'  | 'much' | 'will' | 'would' | 'however' | 'although' | 'though' | 'even' | 'whereas'

ATTRIB	-> 'ATTRIB_0' | 'ATTRIB_1' | 'ATTRIB_2' | 'ATTRIB_3' | 'ATTRIB_4' | 'ATTRIB_5' | 'ATTRIB_6' | 'ATTRIB_7' | 'ATTRIB_8' | 'ATTRIB_9'
INFO_WORD -> 'information' | 'info' | 'data' | 'detail' | 'details'
ENTITY	-> 'ENTITY_0' | 'ENTITY_1' | 'ENTITY_2' | 'ENTITY_3' | 'ENTITY_4' | 'ENTITY_5' | 'ENTITY_6' | 'ENTITY_7' | 'ENTITY_8' | 'ENTITY_9' 

WHERE	-> COND1 | COND1 CONJ2_ WHERE | COND1 WHERE | COND1 CONJ2_ | CONJ2_ WHERE
COND1	-> COND | FILLER2_ COND | COND | FILLER2_ COND FILLER2_ | COND FILLER2_
FILLER2_ -> FILLER2 | FILLER2 FILLER2_
FILLER2	-> FILLER | 'where' | 'with' | 'while' | 'but'
CONJ2_	-> CONJ2 | CONJ2 CONJ2_
CONJ2	-> CONJ1 | 'neither' | 'either' | 'nor' | 'but' | 'not'

COND -> VALUE COMP_ VALUE | COMP_ VALUE | VALUE VALUE | COMP_SP VALUE | VALUE COMP_SP | COMP_SP | COMP_ VALUE VALUE
VALUE -> CONSTANT | ATTRIB
CONSTANT -> 'CONST_0' | 'CONST_1' | 'CONST_2' | 'CONST_3' | 'CONST_4' | 'CONST_5' | 'CONST_6' | 'CONST_7' | 'CONST_8' | 'CONST_9'

COMP_	-> FILLER3_ COMP1 | COMP1 | FILLER3_ COMP1 FILLER3_ | COMP1 FILLER3_
FILLER3_ -> FILLER3 | FILLER3 FILLER3_
FILLER3	-> 'does' | 'do' | 'it' | 'is' | 'to'
COMP1	-> COMP2 | NEGATE COMP2 | NEGATE FILLER3_ COMP2
COMP2	-> COMP3 EXTRA | COMP3
COMP3	-> 'is' | 'in' | 'belongs' | 'belong' | 'belonging' | 'equal' | 'equals' | 'unequal' | 'higher' | 'lower' | 'greater' | 'lesser' | 'more' | 'less' | 'larger' | 'bigger' | 'smaller' | 'greater_than_or_equal' | 'lesser_than_or_equal'| 'divisible' | 'of' | 'same' | 'much' | 'not' | 'as'
EXTRA	-> 'to' | 'than' | 'by' | 'of' | 'as'
NEGATE	-> 'not' | 'neither' | 'nor'

COMP_SP	-> FILLER3_ COMP_SP1 | COMP_SP1
COMP_SP1 -> COMP_SP2 | NEGATE COMP_SP2 | NEGATE FILLER3_ COMP_SP2
COMP_SP2 -> 'maximum' | 'minimum' | 'highest' | 'lowest' | 'least' | 'smallest' | 'largest' | 'greatest' | 'small' | 'low' | 'high' | 'large' | 'big' | 'huge'
